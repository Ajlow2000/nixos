precmd() {
    precmd() {
        echo
    }
}

path+=($XDG_BIN_HOME)

# ~~~ Aliases ~~~
alias gs="git status"
alias ls="eza --group-directories-first"
alias ltr="ls --tree"
alias grep="grep --color=auto"
alias ncu="sudo nix-channel --update"
alias hms="home-manager switch --flake $XDG_CONFIG_HOME/nixos/#$USER"
alias nrs="sudo nixos-rebuild switch --flake $XDG_CONFIG_HOME/nixos/#$HOST"
alias tsm="tmux-session-manager"
alias gcm="conventional-commit"
alias kitty-tmux="kitty tmux-session-manager home"

alias path="toolbox misc print-path"
alias audit="toolbox git audit-dir --ignore-dirs $HOME/.local"
alias add-repo="toolbox git add-repo --path $HOME/repos --url " # add-repo [url]

_fix_cursor() {
   echo -ne '\e[5 q'
}
precmd_functions+=(_fix_cursor)

function chpwd() {
    emulate -L zsh
    zoxide add $(pwd)
}

autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

setopt inc_append_history_time

# opam configuration
[[ ! -r /home/ajlow/.opam/opam-init/init.zsh ]] || source /home/ajlow/.opam/opam-init/init.zsh  > /dev/null 2> /dev/null

# ~~~~~ Direnv ~~~~~
eval "$(direnv hook zsh)"
# Tone down verbosity of loading output
# https://github.com/direnv/direnv/issues/68#issuecomment-1003426550
copy_function() {
  test -n "$(declare -f "$1")" || return
  eval "${_/$1/$2}"
}
copy_function _direnv_hook _direnv_hook__old
_direnv_hook() {
  _direnv_hook__old "$@" 2> >(awk '{if (length >= 200) { sub("^direnv: export.*","direnv: export "NF" environment variables")}}1')
  wait
}

eval "$(zoxide init zsh)"

autoload -U colors && colors

AJLOW_PROMPT="%{$fg[green]%}[%{$fg[blue]%}%n@%{$fg[blue]%}%m%{ $fg[green]%}%~]%{$reset_color%}  "
AJLOW_RPROMPT="%(?..%F{red}%?%f )[%* %D{%Z}]"

_generate_prompt () {
    if [[ -n "$DIRENV_FILE" ]]; then
        PROMPT="(direnv)$AJLOW_PROMPT"
        RPROMPT="$AJLOW_RPROMPT"
    else
        PROMPT="$AJLOW_PROMPT"
        RPROMPT="$AJLOW_RPROMPT"
    fi
}
precmd_functions+=(_generate_prompt)

